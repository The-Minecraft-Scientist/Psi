buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id "com.diffplug.spotless" version "5.7.0"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'pmd'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)

def mc_version = "1.17.1"
version = "${config.version}-${config.build_number}"
group = "vazkii.${config.mod_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '16' // Need this here so eclipse task generates correctly.

if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}

/*mixin {
  add sourceSets.main, "psi.refmap.json"
}*/

minecraft {
    mappings channel: "official", version: "1.17.1"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            properties 'fml.earlyprogresswindow': 'false'
            property 'forge.logging.console.level', 'debug'
            // property 'mixin.env.disableRefMap', 'true'
            // arg "-mixin.config=" + config.mod_id + ".mixins.json"

            mods {
                psi {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            // property 'mixin.env.disableRefMap', 'true'
            // arg "-mixin.config=" + config.mod_id + ".mixins.json"
            singleInstance(true)
            mods {
                psi {
                    source sourceSets.main
                }
            }
        }
        data {
            properties 'forge.logging.console.level': 'debug'
            properties 'fml.earlyprogresswindow': 'false'
            // property 'mixin.env.disableRefMap', 'true'
            // arg "-mixin.config=" + config.mod_id + ".mixins.json"
            workingDirectory project.file('run')
            mods {
                psi {
                    source sourceSets.main
                }
            }
            args '--mod', 'psi', '--all',
                    '--existing', file('src/main/resources/'),
                    '--output', file('src/generated/resources/')
        }
    }
}

repositories {
    mavenCentral()
    maven {
        // for Patchouli
        name "blamejared"
        url "https://maven.blamejared.com/"
    }
    maven {
        // for JEI
        name "promwml16"
        url "https://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "1.17.1-37.1.1" //"${mc_version}-34.1.0"

    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.17.1-58-SNAPSHOT:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.17.1-58-SNAPSHOT")

    compileOnly fg.deobf("mezz.jei:jei-1.17.1:8.3.1.62:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.17.1:8.3.1.62")

}

// Prevent Mixin annotation processor from getting into IDEA's annotation processor settings
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

spotless {
    enforceCheck = false // workaround around jenkins having wrong line endings in some files
    // we explicitly check in github actions builds
    java {
        targetExclude '**/Gamma.java'

        indentWithTabs()
        licenseHeaderFile( 'spotless/license_template.txt')
        eclipse().configFile 'spotless/eclipseformat.xml'
        removeUnusedImports()
        importOrderFile 'spotless/eclipse.importorder'
    }
}

pmd {
    toolVersion '6.22.0'
    incrementalAnalysis.set(true)
    ruleSets.clear()
    ruleSetFiles = files("spotless/pmd-ruleset.xml")
}


task checkSyntax {
    dependsOn spotlessJavaCheck, pmdMain
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}


processResources {
    // replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        // replace version and mcversion
        expand 'version': project.version
    }

    // copy everything else, thats not the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml', '**/psd/**'
    }
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = 'sources'
}

task apiJar(type: Jar) {
    // Remove sources from the api jar when MinecraftForge/ForgeGradle#369 is fixed
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include 'vazkii/psi/api/**'

    classifier = 'api'
}

artifacts {
    archives srcJar, apiJar
}

task incrementBuildNumber {
    doFirst {
        config.build_number = (config.build_number.toString().toInteger()) + 1
        configFile.withWriter {
            config.toProperties().store(it, "")
        }
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

jar {
    //rename the default output, for some better... sanity with scipts
    archiveName = "${baseName} ${version}.${extension}"

    manifest {
        attributes([
                "Specification-Title": "${config.mod_id}",
                "Specification-Vendor": "vazkii",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": "${config.mod_id}",
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"vazkii",
                "MixinConfigs": "${config.mod_id}.mixins.json"
        ])
    }

    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
    exclude "**/unused"
    exclude "**/*.py"
    exclude "**/*.sh"
    exclude "**/*.txt"
    exclude "**/*.sh"
    exclude "**/*.vim"
    exclude "**/mappings-en_us.json"
    exclude "**/en_us_base.json"
}


/*uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}*/

/*mixin {
    add sourceSets.main, "${config.mod_id}.refmap.json"
}*/

defaultTasks 'clean', 'runData', 'checkSyntax', 'build', 'incrementBuildNumber'
